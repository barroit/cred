# SPDX-License-Identifier: GPL-3.0-or-later or MIT

find_package(Python3 3.6 REQUIRED)

# Not that SCP, scoped instead :)
function(include_scp name)
  include(${name})
endfunction()

function(cc_include name)
  if(NOT IS_ABSOLUTE ${name})
    set(name ${CMAKE_CURRENT_SOURCE_DIR}/${name})
  endif()

  # By including our header this way, we avoid option de-duplication by CMake.
  add_compile_options("SHELL:-include ${name}")
endfunction()

function(python3 script)
  if(NOT IS_ABSOLUTE ${script})
    set(script ${CMAKE_CURRENT_SOURCE_DIR}/${script})
  endif()

  execute_process(COMMAND ${Python3_EXECUTABLE}
		  ${script} ${ARGN} COMMAND_ERROR_IS_FATAL ANY)
endfunction()

function(compile_add_sanitizer)
  foreach(target ${ARGN})
    target_compile_options(${target} PRIVATE -fsanitize=address,undefined)
  endforeach()
endfunction()

function(link_add_sanitizer)
  foreach(target ${ARGN})
    target_link_options(${target} PRIVATE -fsanitize=address,undefined)
  endforeach()
endfunction()

function(fixup_runtime target)
  add_custom_command(TARGET ${target} POST_BUILD
		     COMMAND ${CMAKE_COMMAND} -E copy_if_different
		     ${ARGN} $<TARGET_FILE_DIR:${target}>)
endfunction()
