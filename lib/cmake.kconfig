# SPDX-License-Identifier: GPL-3.0-or-later or MIT

if(NOT EXISTS ${TREE}/$ENV{KCONFIG_CONFIG})
  set(ENV{KCONFIG_CONFIG} ${TREE}/$ENV{KCONFIG_CONFIG}.def)
  if(NOT EXISTS ${TREE}/$ENV{KCONFIG_CONFIG})
    python3(scripts/kconfig.py alldefconfig)
  endif()
endif()

# It's okay to have only a .config.xxx.def on disk with $KCONFIG_CONFIG set to
# .config.xxx, as genconfig will look up .config.xxx.def.
add_custom_target(genconfig ${Python3_EXECUTABLE} scripts/kconfig.py
		  genconfig --header-path ${GEN}/autoconf.h
		  DEPENDS ${TREE}/.last_build
		  WORKING_DIRECTORY ${TREE})

file(STRINGS $ENV{KCONFIG_CONFIG} lines ENCODING UTF-8)

foreach(line ${lines})
  if(line MATCHES "^(CONFIG_[^=]+)=(\"?)([^\"]*)(\"?)$")
    set(${CMAKE_MATCH_1} ${CMAKE_MATCH_3} PARENT_SCOPE)
  endif()
endforeach()

set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS ${TREE}/.config)
